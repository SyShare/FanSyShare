apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'walle'
apply plugin: 'com.jakewharton.hugo'

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

android {

    signingConfigs {
        config {
            keyAlias 'keystore'
            keyPassword '123456'
            storeFile file('keystore.jks')
            storePassword '123456'
        }
    }

    compileSdkVersion depends.compileSdkVersion
    defaultConfig {
        applicationId "sy.com.initproject"
        minSdkVersion depends.minSdkVersion
        targetSdkVersion depends.targetSdkVersion
        versionCode 1
        versionName APP_VERSION
        buildConfigField "boolean", "LEO_DEBUG", "true"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        signingConfig signingConfigs.config
        multiDexEnabled true
    }

    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    dataBinding {
        enabled = true
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "6g"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {//对lambda的支持
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            buildConfigField "boolean", "LEO_DEBUG", RELEASE_LEO_DEBUG
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false
            signingConfig signingConfigs.config
            minifyEnabled true
            buildConfigField "boolean", "LEO_DEBUG", RELEASE_LEO_DEBUG
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //工具库
    implementation project(':base')
    implementation project(':core')
    implementation project(':logger')
    implementation project(':util')
    implementation project(':lib-http')
    implementation project(':lib-player')
    implementation project(':lib-skin')

    //依赖库
    implementation depends.baseadapter('2.9.40')
    implementation depends.cardview_v7()

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation('com.alibaba.android:vlayout:1.2.8@aar') {
        transitive = false
    }
    implementation depends.eventbus()

    //分包
    implementation 'com.android.support:multidex:1.0.3'
    //Banner
    implementation 'com.banner:banner:1.0'
}

/**
 * 改变包路径
 * @param type
 */
def changeApkPath(String type, String buldType) {
    def timeNow = getDate()
    android.applicationVariants.all { variant ->
        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
            variant.getPackageApplication().outputDirectory = new File(rootProject.rootDir.getAbsolutePath() + "/output/yxsj/" + APP_VERSION + "/" + type + "/" + timeNow)
            if ("true".equals(IS_JENKINS)) {
                apkData.outputFileName = 'yj-android-v' +
                        APP_VERSION + '-' + variant.flavorName + timeNow + '-' + type + '.apk'
            } else {
                if (variant.buildType.name.equals('debug')) {
                    apkData.outputFileName = "yj-android-v${APP_VERSION}-debug.apk"
                } else {
                    apkData.outputFileName = 'yj-android-v' +
                            APP_VERSION + '-' + variant.flavorName + timeNow + '-' + type + '.apk'
                }
            }
        }
    }
}
/**
 * 编译测试任务
 */
task buildPublish(dependsOn: ['assembleRelease', 'generatepureDebugJavadoc']) {
    dependsOn {
        changeApkPath("publish", "release")
    }
}

/**
 * 编译Beta任务
 */
task buildBeta(dependsOn: 'assembleDebug') {
    dependsOn {
        changeApkPath("beta", "beta")
    }
}
